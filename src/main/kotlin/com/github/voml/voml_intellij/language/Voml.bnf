{
    parserClass = "com.github.voml.voml_intellij.language.parser.VomlParser"
    parserUtilClass = "com.github.voml.voml_intellij.language.psi.VomlParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Voml"
    psiImplClassSuffix = "Impl"
    psiPackage = "com.github.voml.voml_intellij.language.psi"
    psiImplPackage = "com.github.voml.voml_intellij.language.psi.impl"

    elementTypeHolderClass = "com.github.voml.voml_intellij.language.psi.VomlTypes"
    elementTypeClass = "com.github.voml.voml_intellij.language.psi.VomlElementType"
    tokenTypeClass = "com.github.voml.voml_intellij.language.psi.VomlTokenType"

    psiImplUtilClass = "com.github.voml.voml_intellij.language.psi.impl.VomlPsiImplUtil"

    tokens = [
        COMMENT = 'regexp://[^\r\n]*'
        BLOCK_COMMENT = 'regexp:[/][*][^*]*[*]+([^/*][^*]*[*]+)*[/]'
        NULL = 'null'
        INCLUDE = '@include'
        INHERIT = '@inherit'
        AS = "as"

        BOOLEAN = 'regexp:true|false'

        SYMBOL = 'regexp:[A-Za-z_][A-Za-z0-9_]*'
		STRING = "regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\\"|\\\'|\\)*\")"
        BYTE = 'regexp:(0[bBoOxX][0-9A-Fa-f][0-9A-Fa-f_]*)'
        INTEGER = 'regexp:(0|[1-9][0-9_]*)'
        FLOAT = 'regexp:([0-9]+\.[0-9]*([*][*][0-9]+)?)|(\.[0-9]+([Ee][0-9]+)?)'
        SIGN = 'regexp:[+-]'

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ACCENT = '^'
        ANGLE_L = '<'
        ANGLE_R = '>'
        QUOTATION = '"'
        ESCAPE = '\'
        NON_ESCAPE = 'regexp:[^\\]'
        EQ = '='
        NAN = "nan"
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        CITE = '$'
        DOT = '.'
        STAR = '*'
//      SPACE='regexp:\s+'
//      ANY = 'regexp:.'
    ]
}

Voml ::= expression*;

expression ::= scope
    | back_top
    | include_statement
    | inherit_statement
    | insert_pair
    | insert_item
    | SEMICOLON;
// ------------------------------------
inherit_statement ::= INHERIT (SYMBOL | [string_prefix] string_inline);
// -------------------------------G-----
include_statement ::= INCLUDE [string_prefix] string_inline (AS SYMBOL <<paired include_inner>>);
private include_inner ::= SYMBOL*;
symbol_path ::= key_like (DOT key_like)*;
private key_like ::= str | SYMBOL | INTEGER;
key_path ::= [DOT*] symbol_path;
// ------------------------------------
scope ::= <<paired scope_inner>>;
private scope_inner ::= [scope_mark] key_path;
scope_mark ::= ACCENT | ANGLE_L+ | ANGLE_R;
back_top ::= "---";
// ------------------------------------
pair ::= symbol_path eq value;
private eq ::= EQ | COLON;
insert_pair ::= [DOT] pair;
insert_item ::= (DOT|STAR) value;
// ------------------------------------
value ::= NULL | BOOLEAN | num | ref | str | table;
type_hint ::= SYMBOL;
string_prefix ::= SYMBOL;
number_suffix ::= SYMBOL;
// ------------------------------------
private num ::= [SIGN] (INTEGER | DECIMAL) [number_suffix] | [SIGN] BYTE | other_num;
private other_num ::= NAN;
// ------------------------------------
private str ::= [string_prefix] (string_inline|string_multi);
string_inline ::= QUOTATION char* QUOTATION;
string_multi ::= QUOTATION char* QUOTATION;
private char ::= escaped | NON_ESCAPE;
escaped ::= ESCAPE (ESCAPE|NON_ESCAPE);
// ------------------------------------
ref ::= CITE symbol_path;
// ------------------------------------
table ::= [type_hint] <<paired table_inner>>;
private table_inner ::= [table_item (COMMA table_item)* [COMMA]];
private table_item ::= pair | value;
// ------------------------------------
meta paired ::=
    PARENTHESIS_L <<param>> PARENTHESIS_R
  | BRACKET_L <<param>> BRACKET_R
  | BRACE_L <<param>> BRACE_R
